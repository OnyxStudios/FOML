plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT' apply false
}

allprojects {
    apply plugin: 'fabric-loom'

    // Java
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    // Project
    group = project.mod_group
    archivesBaseName = project.mod_name
    version = project.mod_version

    // Dependencies
    dependencies {
        minecraft "com.mojang:minecraft:${mc_version}"
        mappings "net.fabricmc:yarn:${yarn_mappings}"
        modCompile "net.fabricmc:fabric-loader:${loader_version}"
        modCompile "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    }
}

subprojects {
    afterEvaluate {
        jar {
            from "LICENSE.md"
            manifest {
                attributes([
                        "Implementation-Title": project.name,
                        "Implementation-Version": project.version,
                        "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}".toLowerCase(Locale.ROOT),
                        "Built-On-Minecraft": "${mc_version}",
                        "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
                ])
            }
        }

        // Resource processing
        processResources {
            inputs.property 'archivesBaseName', project.archivesBaseName
            inputs.property 'version', project.version

            from(sourceSets.main.resources.srcDirs) {
                include 'fabric.mod.json'
                expand 'modid': project.archivesBaseName,
                        'version': project.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'fabric.mod.json'
            }
        }
    }
}
